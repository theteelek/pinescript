//@version=5
indicator("TeeLek Lab 03 DCA and Pause", overlay=true)

// ----------------------------------------
// Input
// ----------------------------------------
DATE_START = input.time(timestamp("1 July 2024"), title="Start Date",group="Working Date")
DATE_END = input.time(timestamp("1 Aug 2024"), title="End Date",group="Working Date")
DCA_SIZE_BTC = input.float(0.002, title="DCA Size (BTC)",group="Trade Setup")

//---------------------------------
// Function
//--------------------------------- 
hma(src, length) =>
    halfLength = math.round(length / 2)
    sqrtLength = math.round(math.sqrt(length))
    wma1 = ta.wma(src, halfLength)
    wma2 = ta.wma(src, length)
    hmaValue = ta.wma(2 * wma1 - wma2, sqrtLength)
    hmaValue

// ----------------------------------------
// Variable
// ----------------------------------------
var DCA_BTC = float(0)
var DCA_AvgPrice = float(0)
var Long_AvgPrice = float(0)
var Long_BTC = float(0)
var Short_AvgPrice = float(0)
var Short_BTC = float(0)
var diff=float(0)
//------------
var Long_AccomProfit = float(0)
var Long_UnrealizeProfit = float(0)
var Short_AccomProfit = float(0)
var Short_UnrealizeProfit = float(0)
var TotalProfit = float(0)
//------------

//---------------------------------
// Signal
//---------------------------------
// HMA
HMA_Line0 = hma(close,5)
HMA_Line = ta.ema(HMA_Line0,5)
var HMA_LineColor = color(na)
if HMA_Line>HMA_Line[1]
    HMA_LineColor:=#00FF00
else
    HMA_LineColor:=#FF0000

// ----------------------------------------
// Strategy // DCA Every Day
// ----------------------------------------
if time >= DATE_START and time < DATE_END
    if DCA_BTC>0
        DCA_AvgPrice:=((close*DCA_SIZE_BTC) + (DCA_AvgPrice*DCA_BTC))/(DCA_BTC+DCA_SIZE_BTC)
        DCA_BTC:=DCA_BTC+DCA_SIZE_BTC
    else
        DCA_AvgPrice:=close
        DCA_BTC:=DCA_SIZE_BTC

// ----------------------------------------
// Strategy // DCA Long
// ----------------------------------------
if time >= DATE_START and time < DATE_END
    if Long_BTC==0 and HMA_Line[1]<HMA_Line
        diff:=DCA_BTC-Long_BTC+DCA_SIZE_BTC
        if diff>0
            Long_AvgPrice:=((close*diff)+(Long_AvgPrice*Long_BTC))/(Long_BTC+diff)
            Long_BTC:=Long_BTC+diff
    if Long_BTC>0
        diff:=DCA_BTC-Long_BTC+DCA_SIZE_BTC
        if diff>0
            Long_AvgPrice:=((close*diff)+(Long_AvgPrice*Long_BTC))/(Long_BTC+diff)
            Long_BTC:=Long_BTC+diff
    if HMA_Line[1]>HMA_Line and Long_BTC>0 and close<Long_AvgPrice 
        Long_AccomProfit:=Long_AccomProfit+(close*Long_BTC)-(Long_AvgPrice*Long_BTC)
        Long_AvgPrice:=0
        Long_BTC:=0

// ----------------------------------------
// Strategy // DCA Short
// ----------------------------------------
if time >= DATE_START and time < DATE_END
    if Short_BTC==0 and HMA_Line[1]>HMA_Line
        diff:=DCA_BTC-Short_BTC+DCA_SIZE_BTC
        if diff>0
            Short_AvgPrice:=((close*diff)+(Short_AvgPrice*Short_BTC))/(Short_BTC+diff)
            Short_BTC:=Short_BTC+diff
    if Short_BTC>0
        diff:=DCA_BTC-Short_BTC+DCA_SIZE_BTC
        if diff>0
            Short_AvgPrice:=((close*diff)+(Short_AvgPrice*Short_BTC))/(Short_BTC+diff)
            Short_BTC:=Short_BTC+diff
    if HMA_Line[1]<HMA_Line and Short_BTC>0 and close>Short_AvgPrice 
        Short_AccomProfit:=Short_AccomProfit+(Short_AvgPrice*Short_BTC)-(close*Short_BTC)
        Short_AvgPrice:=0
        Short_BTC:=0

// ----------------------------------------
// Calculate UnrealizeProfit 
// ----------------------------------------
if time >= DATE_START and time < DATE_END 
    Long_UnrealizeProfit:=(close*Long_BTC)-(Long_AvgPrice*Long_BTC)
    Short_UnrealizeProfit:=(Short_AvgPrice*Short_BTC)-(close*Short_BTC)
    TotalProfit:=Long_UnrealizeProfit+Short_UnrealizeProfit+Long_AccomProfit+Short_AccomProfit

// ----------------------------------------
// Draw
// ----------------------------------------
// dca line
plot(DCA_AvgPrice, "DCA_AvgPrice", DCA_AvgPrice[1]>0 and DCA_AvgPrice>0 ? #1368de : na  , linewidth=2)
plot(Long_AvgPrice, "Long Avg Line", Long_AvgPrice[1]>0 and Long_AvgPrice>0 ? #8bf519 : na  , linewidth=2)
plot(Short_AvgPrice, "Short Avg Price", Short_AvgPrice[1]>0 and Short_AvgPrice>0 ? #ee0f0f : na  , linewidth=2)

// HMA Line 
plot(HMA_Line, color=HMA_LineColor, title="Medium Trend", linewidth=1)
